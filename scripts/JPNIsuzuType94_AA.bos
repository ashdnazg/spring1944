#include "constants.h"

piece  base, turret, sleeve, barrel, flare, wheels1, wheels2, wheels3;

static-var  restore_delay, rock, aim2, reloading, mainHead, bMoving, GunAiming, MGAiming, CannonFearOk, MGFearOk, shotCount;

// Signal definitions
#define SIG_MOVE	1
#define SIG_AIM		2 // Main Cannon
#define SIG_AIM_2	4 // Coax MG

// definitions for SmokeUnit_GPL
#define SMOKEPIECE1	base
#define SMOKEPIECE2 turret
#define SMOKEPIECE3 sleeve
#define SMOKEPIECE4 base

#include "SmokeUnit_GPL.h" // Thanks again to Argh

//hiding FX emit points
#define FX_HIDE\
	hide flare;\

// definitions for RockUnit_Set
#define ROCK_SPEED	<50> // heavier units should rock less
#define RESTORE_SPEED	<20>
#define ROCKVAR		rock

#include "RockUnit_Set.h"

// definitions for HitWeapId_S44
#define ARMOUR		1109
#include "HitWeapId_S44.h"

// definitions for wheelss
#define wheels_SPIN		<500>
#define wheels_ACCEL		<150>

// definitions for turret movement
#define TURR_TRAVERSE	<400> // manual
#define TURR_ELEVATE	<300>
#define DEPLOY_SPEED	<100> // speed which the sideflaps fall

// definitions for weapons behaviour
#define RELOAD		300 // time in milliseconds between shots
#define RECOIL		[-1] // distance to recoil the gun
#define RETURN_SPEED	[20] // speed at which the barrel returns
#define BURST1		3
#define BURST2		5

//effects
#define placeholder_EFFECT01 1024+1
#define placeholder_EFFECT02 1024+2
#define placeholder_EFFECT03 1024+3
#define placeholder_EFFECT04 1024+4
#define placeholder_EFFECT05 1024+5
#define placeholder_EFFECT06 1024+6
#define MG_MUZZLEFLASH 1024+7
#define XSMALL_MUZZLEFLASH 1024+8
#define XSMALL_MUZZLEDUST 1024+9

//tracers
#define LARGE_TRACER		2048+1
#define TRACER_PER_X_SHOTS	3

Create()
{
	FX_HIDE
	// init vars
	rock = FALSE;
	aim2 = FALSE;
	reloading = FALSE;
	shotCount=0;
		// start continuous scripts
	start-script SmokeUnit_GPL();
}

StartMoving()
{
	signal SIG_MOVE;
	bMoving = TRUE;

 	spin wheels1 around x-axis speed wheels_SPIN accelerate wheels_ACCEL;
 	spin wheels2 around x-axis speed wheels_SPIN accelerate wheels_ACCEL;
 	spin wheels3 around x-axis speed wheels_SPIN accelerate wheels_ACCEL;
}

StopMoving()
{
	signal SIG_MOVE;
	bMoving = FALSE;

	stop-spin wheels1 around x-axis;
 	stop-spin wheels2 around x-axis;
 	stop-spin wheels3 around x-axis;
}

SetMaxReloadTime(longestReloadTime)
{
	restore_delay = longestReloadTime * 2;
}

RestoreAfterDelay()
{
	sleep restore_delay;
	turn turret to y-axis 0 speed <60>;
	turn barrel to x-axis 0 speed <10>;
}

Reload()
{
	reloading = TRUE;
	sleep RELOAD;
	reloading = FALSE;
}

SweetSpot(piecenum)
{
	piecenum = base;
}

// Weapon 1 - Main cannon, HE
QueryWeapon1(piecenum)
{
	piecenum = flare;
}

AimFromWeapon1(piecenum)
{
	piecenum = turret;
}

AimWeapon1(heading, pitch)
{
	if (aim2) return 0; // don't aim weap1 if aiming AP
	
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	GunAiming=1;
	
	mainHead = heading;
	
	turn turret to y-axis heading speed TURR_TRAVERSE;
	turn sleeve to x-axis 0 - pitch speed TURR_ELEVATE;
	wait-for-turn turret around y-axis;
	wait-for-turn sleeve around x-axis;
	
	start-script RestoreAfterDelay();
	if (reloading) return 0; // aim first, then pause if the shot cannot fire
	GunAiming=0;
	return 1;
}

FireWeapon1()
{
	CannonFearOk=1;
	start-script Reload(); // start reload timer after firing
	return 0;
}

Shot1(just_a_zero)
{
	++shotCount;
	if(shotCount>=TRACER_PER_X_SHOTS)
	{
		emit-sfx LARGE_TRACER from flare;
		shotCount=0;
	}
	emit-sfx XSMALL_MUZZLEFLASH from flare;
	emit-sfx XSMALL_MUZZLEDUST from flare;
	// barrel recoil
	move barrel to z-axis RECOIL now;
	sleep 50;
	move barrel to z-axis 0 speed RETURN_SPEED;
}

Killed(severity, corpsetype)
{
	if(severity <= 50)
	{
		corpsetype = 1;
		return 0;
	}
	if(severity <= 999)
	{
		corpsetype = 2;
		explode turret type EXPLODE_ON_HIT | FIRE | FALL;
		explode wheels1 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT;
		explode wheels2 type SHATTER | EXPLODE_ON_HIT;
		explode wheels3 type SHATTER | EXPLODE_ON_HIT;
		return 0;
	}
	corpsetype = 3;
	explode turret type EXPLODE_ON_HIT | FIRE | FALL;
	explode barrel type SHATTER;
	explode wheels1 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT;
	explode wheels2 type SHATTER | EXPLODE_ON_HIT;
	explode wheels3 type SHATTER | EXPLODE_ON_HIT;
}

