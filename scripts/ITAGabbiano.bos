#define TAK

piece base, hull, railings, boats,
	  main_turret, main_sleeve, main_barrel, main_flare,
	  torp1, torp_flare1, torp2, torp_flare2,
	  aa20_turret_1, aa20_sleeve_1, aa20_barrel_1, aa20_flare_1,
	  aa20_turret_2, aa20_sleeve_2, aa20_barrel_2, aa20_flare_2,
	  aa20_turret_3, aa20_sleeve_3, aa20_barrel_3, aa20_flare_3,
	  aa20_turret_4, aa20_sleeve_4, aa20_gun41, aa20_flare41, aa20_gun42, aa20_flare42,
	  aa20_turret_5, aa20_sleeve_5, aa20_gun51, aa20_flare51, aa20_gun52, aa20_flare52,
	  exhaust, wake1, wake2, wake3, wake4;

#define BASE_SMOKE_PERIOD	40
#define MIN_SMOKE_PERIOD	240

//********************Turret turn speeds
#define MAIN_TURRET_TURN		<25>
#define MAIN_TURRET_PITCH		<25>

#define TORP_TURRET_TURN		<5>

#define AA20_TURRET_TURN		<30>
#define AA20_TURRET_PITCH		<45>

#define MAIN_MAX_ANGLE			<60>

//*********************Recoil
#define MAIN_GUN_RECOIL			[1]
#define MAIN_GUN_RECOIL_SPEED	[10]
#define MAIN_GUN_BACK_SPEED		[2]

#define AA20_GUN_RECOIL			[0.3]
#define AA20_GUN_RECOIL_SPEED	[10]
#define AA20_GUN_BACK_SPEED		[3]

//*********************Death anim
#define HULL_LIST_SPEED			<10>
#define HULL_LIST_ANGLE			<-45>

//********************Engine smoke
#define ENGINE_SMOKE_PIECE	exhaust
#define ENGINE_SMOKE_PERIOD	200

#define RESTORE_PERIOD		2500

#define SIG_DEATH			1
#define SIG_MOVE			2
#define SIG_AIM1			4
#define SIG_AIM2			8
#define SIG_AIM3			16
#define SIG_AIM4			32
#define SIG_AIM5			64
#define SIG_AIM6			128
#define SIG_AIM7			256
#define SIG_AIM8			512

#define ALL_WEAPON_SIGS		SIG_AIM1|SIG_AIM2|SIG_AIM3|SIG_AIM4|SIG_AIM5|SIG_AIM6|SIG_AIM7|SIG_AIM8

#define SMOKEPIECE	hull

#include "exptype.h"
#include "sfxtype.h"
#include "damageSmoke.h"

static-var	CurrentSpeed, bMoving, aa20_flare4, aa20_flare5,
			oldXZ, oldHeading;

WorkingLoop()
{
	while(TRUE)
	{
		if(bMoving)
		{
			// engine smoke comes here. And engine sound.
			emit-sfx SFXTYPE_BLACKSMOKE from ENGINE_SMOKE_PIECE;
			sleep ENGINE_SMOKE_PERIOD;
		}
		sleep 500;
	}
}

InitTurrets()
//Pre-position turrets
{
	turn aa20_turret_4 to y-axis <180> now;
	turn aa20_turret_5 to y-axis <180> now;
	aa20_flare4=TRUE;
	aa20_flare5=TRUE;
}

WaitForConstructionToFinish()
{
	while(get BUILD_PERCENT_LEFT)
	{
		sleep 150;
	}
}

Create()
{
	bMoving = FALSE;
	call-script InitTurrets();
	call-script WaitForConstructionToFinish();
	start-script DamageSmoke();
	start-script WorkingLoop();
}

SweetSpot(piecenum)
{
	piecenum = hull;
}

EmitWakes()
{
	set-signal-mask SIG_MOVE|SIG_DEATH;
	while( TRUE )
	{
		emit-sfx 2 from wake1;
		emit-sfx 2 from wake2;
		emit-sfx 2 from wake3;
		emit-sfx 2 from wake4;
		sleep 300;
	}
}

StopMoving()
{
	signal SIG_MOVE;
	set-signal-mask SIG_MOVE;
	// We don't want this to trigger on VERY short stops
	sleep 300;
	bMoving = FALSE;
}

StopCatcher()
{
	var newXZ, newHeading;
	set-signal-mask SIG_MOVE|SIG_DEATH;
	while(TRUE)
	{
		sleep 300;
		newXZ = get PIECE_XZ(base);
		newHeading = get HEADING;
		if((oldXZ == newXZ) && (oldHeading == newHeading))
		{
			call-script StopMoving();
		}
		oldXZ = newXZ;
		oldHeading = newHeading;
	}
}

StartMoving()
{
	signal SIG_MOVE;
	start-script EmitWakes();
	start-script StopCatcher();
	bMoving = TRUE;
}

RestoreTurret()
{
	set-signal-mask SIG_DEATH|ALL_WEAPON_SIGS;
	sleep RESTORE_PERIOD;

	turn main_turret to y-axis <0> speed MAIN_TURRET_TURN;
	turn main_sleeve to x-axis <0> speed MAIN_TURRET_PITCH;

	turn torp1 to y-axis <0> speed TORP_TURRET_TURN;
	turn torp2 to y-axis <0> speed TORP_TURRET_TURN;
	
	turn aa20_turret_1 to y-axis <0> speed AA20_TURRET_TURN;
	turn aa20_sleeve_1 to x-axis <0> speed AA20_TURRET_PITCH;
	turn aa20_turret_2 to y-axis <0> speed AA20_TURRET_TURN;
	turn aa20_sleeve_2 to x-axis <0> speed AA20_TURRET_PITCH;
	turn aa20_turret_3 to y-axis <0> speed AA20_TURRET_TURN;
	turn aa20_sleeve_3 to x-axis <0> speed AA20_TURRET_PITCH;
	turn aa20_turret_4 to y-axis <180> speed AA20_TURRET_TURN;
	turn aa20_sleeve_4 to x-axis <0> speed AA20_TURRET_PITCH;
	turn aa20_turret_5 to y-axis <180> speed AA20_TURRET_TURN;
	turn aa20_sleeve_5 to x-axis <0> speed AA20_TURRET_PITCH;
}

QueryWeapon1(piecenum)
{
	piecenum = main_flare;
}

AimFromWeapon1(piecenum)
{
	piecenum = main_turret;
}

AimWeapon1(heading, pitch)
{
	signal SIG_AIM1;
	set-signal-mask SIG_AIM1|SIG_DEATH;
	if(pitch>MAIN_MAX_ANGLE)
	{
		// retarget, this one is too high
		set CHANGE_TARGET to 1;
		return FALSE;
	}
	turn main_turret to y-axis heading speed MAIN_TURRET_TURN;
	turn main_sleeve to x-axis (0-pitch) speed MAIN_TURRET_PITCH;
	wait-for-turn main_turret around y-axis;
	wait-for-turn main_sleeve around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon1()
{
	show main_flare;
	move main_barrel to z-axis (0-MAIN_GUN_RECOIL) speed MAIN_GUN_RECOIL_SPEED;
	sleep 200;
	move main_barrel to z-axis [0] speed MAIN_GUN_BACK_SPEED;
	hide main_flare;
}

QueryWeapon2(piecenum)
{
	piecenum = aa20_flare_1;
}

AimFromWeapon2(piecenum)
{
	piecenum = aa20_turret_1;
}

AimWeapon2(heading, pitch)
{
	signal SIG_AIM2;
	set-signal-mask SIG_AIM2|SIG_DEATH;
	turn aa20_turret_1 to y-axis heading speed AA20_TURRET_TURN;
	turn aa20_sleeve_1 to x-axis (0-pitch) speed AA20_TURRET_PITCH;
	wait-for-turn aa20_turret_1 around y-axis;
	wait-for-turn aa20_sleeve_1 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon2()
{
	show aa20_flare_1;
	sleep 200;
	hide aa20_flare_1;
}

Shot2(foo)
{
	move aa20_barrel_1 to z-axis (0-AA20_GUN_RECOIL) speed AA20_GUN_RECOIL_SPEED;
	sleep 200;
	move aa20_barrel_1 to z-axis [0] speed AA20_GUN_BACK_SPEED;
}

QueryWeapon3(piecenum)
{
	piecenum = aa20_flare_2;
}

AimFromWeapon3(piecenum)
{
	piecenum = aa20_turret_2;
}

AimWeapon3(heading, pitch)
{
	signal SIG_AIM3;
	set-signal-mask SIG_AIM3|SIG_DEATH;
	turn aa20_turret_2 to y-axis heading speed AA20_TURRET_TURN;
	turn aa20_sleeve_2 to x-axis (0-pitch) speed AA20_TURRET_PITCH;
	wait-for-turn aa20_turret_2 around y-axis;
	wait-for-turn aa20_sleeve_2 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon3()
{
	show aa20_flare_2;
	sleep 200;
	hide aa20_flare_2;
}

Shot3(foo)
{
	move aa20_barrel_2 to z-axis (0-AA20_GUN_RECOIL) speed AA20_GUN_RECOIL_SPEED;
	sleep 200;
	move aa20_barrel_2 to z-axis [0] speed AA20_GUN_BACK_SPEED;
}

QueryWeapon4(piecenum)
{
	piecenum = aa20_flare_3;
}

AimFromWeapon4(piecenum)
{
	piecenum = aa20_turret_3;
}

AimWeapon4(heading, pitch)
{
	signal SIG_AIM4;
	set-signal-mask SIG_AIM4|SIG_DEATH;
	turn aa20_turret_3 to y-axis heading speed AA20_TURRET_TURN;
	turn aa20_sleeve_3 to x-axis (0-pitch) speed AA20_TURRET_PITCH;
	wait-for-turn aa20_turret_3 around y-axis;
	wait-for-turn aa20_sleeve_3 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon4()
{
	show aa20_flare_3;
	sleep 200;
	hide aa20_flare_3;
}

Shot4(foo)
{
	move aa20_barrel_3 to z-axis (0-AA20_GUN_RECOIL) speed AA20_GUN_RECOIL_SPEED;
	sleep 200;
	move aa20_barrel_3 to z-axis [0] speed AA20_GUN_BACK_SPEED;
}

QueryWeapon5(piecenum)
{
	if(aa20_flare4)
	{
		piecenum = aa20_flare41;
	} else
	{
		piecenum = aa20_flare42;
	}
}

AimFromWeapon5(piecenum)
{
	piecenum = aa20_turret_4;
}

AimWeapon5(heading, pitch)
{
	signal SIG_AIM5;
	set-signal-mask SIG_AIM5|SIG_DEATH;
	turn aa20_turret_4 to y-axis heading speed AA20_TURRET_TURN;
	turn aa20_sleeve_4 to x-axis (0-pitch) speed AA20_TURRET_PITCH;
	wait-for-turn aa20_turret_4 around y-axis;
	wait-for-turn aa20_sleeve_4 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

Shot5(foo)
{
	if(aa20_flare4)
	{
		show aa20_flare41;
		move aa20_gun41 to z-axis (0-AA20_GUN_RECOIL) speed AA20_GUN_RECOIL_SPEED;
		move aa20_gun41 to z-axis [0] speed AA20_GUN_BACK_SPEED;
		hide aa20_flare41;
	} else
	{
		show aa20_flare42;
		move aa20_gun42 to z-axis (0-AA20_GUN_RECOIL) speed AA20_GUN_RECOIL_SPEED;
		move aa20_gun42 to z-axis [0] speed AA20_GUN_BACK_SPEED;
		hide aa20_flare42;
	}
	aa20_flare4 = !aa20_flare4;
}

QueryWeapon6(piecenum)
{
	if(aa20_flare5)
	{
		piecenum = aa20_flare51;
	} else
	{
		piecenum = aa20_flare52;
	}
}

AimFromWeapon6(piecenum)
{
	piecenum = aa20_turret_5;
}

AimWeapon6(heading, pitch)
{
	signal SIG_AIM6;
	set-signal-mask SIG_AIM6|SIG_DEATH;
	turn aa20_turret_5 to y-axis heading speed AA20_TURRET_TURN;
	turn aa20_sleeve_5 to x-axis (0-pitch) speed AA20_TURRET_PITCH;
	wait-for-turn aa20_turret_5 around y-axis;
	wait-for-turn aa20_sleeve_5 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

Shot6(foo)
{
	if(aa20_flare5)
	{
		show aa20_flare51;
		move aa20_gun51 to z-axis (0-AA20_GUN_RECOIL) speed AA20_GUN_RECOIL_SPEED;
		move aa20_gun51 to z-axis [0] speed AA20_GUN_BACK_SPEED;
		hide aa20_flare51;
	} else
	{
		show aa20_flare52;
		move aa20_gun52 to z-axis (0-AA20_GUN_RECOIL) speed AA20_GUN_RECOIL_SPEED;
		move aa20_gun52 to z-axis [0] speed AA20_GUN_BACK_SPEED;
		hide aa20_flare52;
	}
	aa20_flare5 = !aa20_flare5;
}


QueryWeapon7(piecenum)
{
	piecenum = torp_flare1;
}

AimFromWeapon7(piecenum)
{
	piecenum = torp1;
}

AimWeapon7(heading, pitch)
{
	signal SIG_AIM7;
	set-signal-mask SIG_AIM7|SIG_DEATH;
	turn torp1 to y-axis heading speed TORP_TURRET_TURN;
	wait-for-turn torp1 around y-axis;
	start-script RestoreTurret();
	return TRUE;
}

FireWeapon7()
{
	sleep 50;
}

QueryWeapon8(piecenum)
{
	piecenum = torp_flare2;
}

AimFromWeapon8(piecenum)
{
	piecenum = torp2;
}

AimWeapon8(heading, pitch)
{
	signal SIG_AIM8;
	set-signal-mask SIG_AIM8|SIG_DEATH;
	turn torp2 to y-axis heading speed TORP_TURRET_TURN;
	wait-for-turn torp2 around y-axis;
	start-script RestoreTurret();
	return TRUE;
}

FireWeapon8()
{
	sleep 50;
}

Killed(severity, corpsetype)
{
	signal SIG_DEATH;
	if (severity<99)
	{
		corpsetype = 1;
		turn hull to z-axis (HULL_LIST_ANGLE) speed HULL_LIST_SPEED;
		wait-for-turn hull around z-axis;
		return (TRUE);
	}
	corpsetype = 2;
	explode hull type		SHATTER|BITMAP1;
	return (TRUE);
}