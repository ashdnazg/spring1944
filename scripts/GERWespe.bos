#include "STANDARD_COMMANDS_GPL.h" // Thank you Argh

piece base, sleeve, barrel, flare, brakeright, brakeleft, aaref, aaturret, aagun, aaflare, exhaust;
piece frontwheel, rearwheel, wheel1, wheel2, wheel3, wheel4, wheel5, tracks1, tracks2;
 
static-var  restore_delay, rock, reloading, mainHead, bMoving, treadnum, gunaiming, MGAiming;

//how long the unit appears on the map after firing (in ms)
#define VISIBLE_PERIOD	5000

// Signal definitions
#define SIG_AIM		2 // Main Cannon
#define SIG_AIM_2	4 // Coax MG
#define SIG_AIM_3	8 // AA MG

// definitions for SmokeUnit_GPL
#define SMOKEPIECE1	base
#define SMOKEPIECE2 sleeve
#define SMOKEPIECE3 frontwheel
#define SMOKEPIECE4 exhaust

#include "SmokeUnit_GPL.h" // Thanks again to Argh

//hiding FX emit points
#define FX_HIDE\
	hide flare;\
	hide brakeright;\
	hide brakeleft;\
	hide aaflare;\

// definitions for RockUnit_Set
#define ROCK_SPEED	<50> // heavier units should rock less
#define RESTORE_SPEED	<20>
#define ROCKVAR		rock

#include "RockUnit_Set.h"

// definitions for wheels
#define WHEEL_SPIN		<600>
#define WHEEL_ACCEL		<150>

// definitions for turret movement
#define TURR_TRAVERSE	<15>
#define TURR_ELEVATE	<7>
#define AA_TRAVERSE		<225>
#define AA_ELEVATE		<150>
#define COAX_ARC		<6>

// definitions for weapons behaviour
#define RELOAD		3500 // time in milliseconds between shots
#define RECOIL		[-2.5] // distance to recoil the gun
#define RETURN_SPEED	[4] // speed at which the barrel returns

// definitions for HitWeapId_S44
#define ARMOUR		3739
#include "HitWeapId_S44.h"

//effects
#define placeholder_EFFECT01 1024+1
#define placeholder_EFFECT02 1024+2
#define placeholder_EFFECT03 1024+3
#define placeholder_EFFECT04 1024+4
#define placeholder_EFFECT05 1024+5
#define placeholder_EFFECT06 1024+6
//#define MG_MUZZLEFLASH 1024+7
#define LARGE_Muzzleflash	1024+7
#define LARGE_MuzzleDust	1024+8
#define MuzzleBrakeSmoke	1024+9


treads() 
{ 
   while( TRUE ) 
   { 
        if(bMoving) 
           { 
              if(treadnum == 0) 
              {             
              hide tracks2; 
              show tracks1; 
              } 
            if(treadnum == 1) 
            { 
            hide tracks1; 
            show tracks2; 
			}
            treadnum = !treadnum; 
         } 
      sleep 90; 
   } 
}

Create()
{
	set STEALTH to 1;
	FX_HIDE
	// init vars
	rock = FALSE;
	treadnum=0;
	// start continuous scripts
	start-script SmokeUnit_GPL();
	start-script treads();
}

StartMoving()
{
	bMoving = TRUE;
	spin frontwheel around x-axis speed WHEEL_SPIN accelerate WHEEL_ACCEL;
 	spin rearwheel around x-axis speed WHEEL_SPIN accelerate WHEEL_ACCEL;
 	spin wheel1 around x-axis speed WHEEL_SPIN accelerate WHEEL_ACCEL;
 	spin wheel2 around x-axis speed WHEEL_SPIN accelerate WHEEL_ACCEL;
 	spin wheel3 around x-axis speed WHEEL_SPIN accelerate WHEEL_ACCEL;
 	spin wheel4 around x-axis speed WHEEL_SPIN accelerate WHEEL_ACCEL;
 	spin wheel5 around x-axis speed WHEEL_SPIN accelerate WHEEL_ACCEL;
}

StopMoving()
{
	bMoving = FALSE;
	stop-spin frontwheel around x-axis;
 	stop-spin rearwheel around x-axis;
	stop-spin wheel1 around x-axis;
 	stop-spin wheel2 around x-axis;
 	stop-spin wheel3 around x-axis;
 	stop-spin wheel4 around x-axis;
 	stop-spin wheel5 around x-axis;
}

SetMaxReloadTime(longestReloadTime)
{
	restore_delay = longestReloadTime * 2;
}

RestoreAfterDelay()
{
	sleep restore_delay;
	turn barrel to x-axis <0.000000> speed <10>;
}

SweetSpot(piecenum)
{
	piecenum = base;
}

// Weapon 1 - Main cannon, HE
QueryWeapon1(piecenum)
{
	piecenum = flare;
}

AimFromWeapon1(piecenum)
{
	piecenum = base;
}

AimWeapon1(heading, pitch)
{
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	GunAiming=1;
	mainHead = heading;
	turn sleeve to x-axis 0 - pitch speed TURR_ELEVATE;
	wait-for-turn sleeve around x-axis;
	
	start-script RestoreAfterDelay();
	GunAiming=0;
	return 1;
}

FireWeapon1()
{
	rock = TRUE; // firing a heavy weapon
	
	// TODO: Add to SFX
	// Do SFX
	//emit-sfx 1  from flare;
	emit-sfx LARGE_Muzzleflash from flare;
	emit-sfx LARGE_MuzzleDust from flare;
	emit-sfx MuzzleBrakeSmoke from brakeright;
	emit-sfx MuzzleBrakeSmoke from brakeleft;
	// barrel recoil
	move barrel to z-axis RECOIL now;
	sleep 200;
	move barrel to z-axis 0 speed RETURN_SPEED;
	
	rock = FALSE;
	set STEALTH to 0;
	sleep VISIBLE_PERIOD;
	set STEALTH to 1;
	return 0;
}


QueryWeapon2(piecenum)// Weapon 4 - AA MG
	{
	piecenum = aaflare;
	}

AimFromWeapon2(piecenum)
	{
	piecenum = aagun;
	}

AimWeapon2(heading, pitch)
	{
	signal SIG_AIM_2;
	set-signal-mask SIG_AIM_2;

	// thanks to Fang and zwzsg for this!
	turn aaturret to y-axis heading speed AA_TRAVERSE; 
	turn aagun to x-axis 0 - pitch speed AA_ELEVATE; 
	wait-for-turn aaturret around y-axis; 
	wait-for-turn aagun around x-axis;
	
	start-script RestoreAfterDelay();
	return (1);
	}

FireWeapon2()
	{
	rock = FALSE;
	
	// TODO: nice SFX
	show aaflare;
	sleep 50;
	hide aaflare;
	
	return(0);
	}

AimFromWeapon3(piecenum) //bulletproof effect
{
	piecenum = base;
}

QueryWeapon3(piecenum)
{
	piecenum = base;
}

AimWeapon3(heading, pitch)
{
return (1);
}

FireWeapon3()
{
return (0);
}


// TODO: make this pretty
Killed(severity, corpsetype)
{
	if( severity <= 50 )
	{
		corpsetype = 1;
		explode wheel1 type SHATTER | EXPLODE_ON_HIT;
		explode wheel2 type SHATTER | EXPLODE_ON_HIT;
		explode wheel3 type SHATTER | EXPLODE_ON_HIT;
		explode wheel4 type SHATTER | EXPLODE_ON_HIT;
		explode wheel5 type SHATTER | EXPLODE_ON_HIT;
		return (0);
	}
	if( severity <= 250 )
	{
		corpsetype = 1;
		explode wheel1 type SHATTER | EXPLODE_ON_HIT;
		explode wheel2 type SHATTER | EXPLODE_ON_HIT;
		explode wheel3 type SHATTER | EXPLODE_ON_HIT;
		explode wheel4 type SHATTER | EXPLODE_ON_HIT;
		explode wheel5 type SHATTER | EXPLODE_ON_HIT;
	}
	if(severity <= 1000)
	{
		corpsetype = 2;
		explode wheel1 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT;
		explode wheel4 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT;
		explode wheel2 type SHATTER | EXPLODE_ON_HIT;
		explode wheel3 type SHATTER | EXPLODE_ON_HIT;
		explode wheel5 type SHATTER | EXPLODE_ON_HIT;
	}
	corpsetype = 2;
	explode barrel type SHATTER;
	explode frontwheel type SHATTER;
	explode rearwheel type SHATTER;
	explode wheel1 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT;
	explode wheel4 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT;
	explode wheel2 type SHATTER | EXPLODE_ON_HIT;
	explode wheel3 type SHATTER | EXPLODE_ON_HIT;
	explode wheel5 type SHATTER | EXPLODE_ON_HIT;
}

